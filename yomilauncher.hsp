// Yomigaeru Launcher by takuya matsubara
#include "hspda.as"
#define LISTMAX 100

#define GWIDTH 640
#define GHEIGHT 480
screen 0,GWIDTH,GHEIGHT

appname = "Yomigaeru Launcher"
csvname = "yomilauncher.csv"

sdim machine,10,LISTMAX		// machine
sdim softname,30,LISTMAX	// soft name
sdim fnamearray,90,LISTMAX	// file name
sdim book,15,LISTMAX		// book name
sdim templist,LISTMAX

sdim temp,500			// テンポラリ
sdim cline,500			// 1行データバッファ
sdim csv,500*LISTMAX	// csvデータバッファ
	
bload csvname,csv		// CSVファイルの読み込み
notesel csv	//変数をメモリノートパッド命令のバッファに設定
csvmax = notemax	// 要素数

num = 0
for i,0,csvmax
	noteget cline,i		// 指定行を読み込み 1行データバッファ
	csvnote temp, cline	// CSVの１行をメモリノート変換
	split temp, "\n", a, b, c,d
	if(instr(a,0,"MSX")>=0):_continue	// if MSX then skip
	if(a == ""):_continue	// if no data then skip
	machine(num) = a
	softname(num) = b
	fnamearray(num) = c
	book(num) = d
	num++
next
title appname
gosub *sort1

// free buffer
sdim temp,0		// テンポラリ
sdim cline,0	// 1行データバッファ
sdim csv,0		// csvデータバッファ
stop

//
*redrawlist
	cls
	sortstr templist,0

	objsize 100,25
	x=0
	y=0
	pos x,y
	button gosub "Sort by Machine",*sort1

	x+=110
	pos x,y
	button gosub "Sort by Name",*sort2

	x+=110
	pos x,y
	button gosub "Sort by Book",*sort3

	txt=""
	for i,0,num
		if(i > 0):txt+="\n"
		txt += templist(i)
	next

	x=0
	y+=30
	pos x,y
	objsize 400,200
	sellist = 0
	listbox sellist,150,txt

	objsize 400,25
	button gosub "launch",*launch
	stop

//------
*sort1
	for i,0,num
		templist(i) = "["+machine(i)+"] "+"『"+softname(i)+"』 "+"("+book(i)+") "
	next
	gosub *redrawlist
	return
return
	
//------
*sort2
	for i,0,num
		templist(i) = "『"+softname(i)+"』 "+"["+machine(i)+"] "+"("+book(i)+") "
	next
	gosub *redrawlist
return

//------
*sort3
	for i,0,num
		templist(i) = "("+book(i)+") "+"『"+softname(i)+"』 "+"["+machine(i)+"] "
	next
	gosub *redrawlist
return
//------
*launch
temp = templist(sellist)
ptr = instr(temp,0,"『")
if(ptr<0){
	dialog "error1"
	return
}
ptr+=2
a = instr(temp,ptr,"』")
if(a<0){
	dialog "error2"
	return
}
temp = strmid(temp,ptr,a)
for i,0,num
	if softname(i)==temp:_break
next
if(i>=num){
	dialog "not found:"+temp
	return
}

filename = fnamearray(i)
ptr = strlen(filename)
repeat
	if(ptr==0):break
	if(peek(filename,ptr-1)==$5c):break 	// slash
	ptr--
loop
pathname = strmid(filename,0,ptr-1)
exename = strmid(filename,ptr,255)

x=8
y=GHEIGHT-52
font "ＭＳ ゴシック",12
color 0,0,0
boxf x,y,x+600,y+50
color 255,255,0
pos x,y
print "title:'"+temp+"'"
print "path:'"+pathname+"'"
print "filename:'"+exename+"'"

chdir pathname //ディレクトリ移動
exec exename	//Windowsのファイルを実行する
return
